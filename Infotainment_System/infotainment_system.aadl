package infotainment_system
public
--import the external packages
with HMI;
with Hardware;
with software;
with SEI;

--define the system
system infotainment_sys
	features
		power: in feature;
		display :out feature;
	properties
		SEI::PowerBudget => 2.5 w applies to power;
	--system error handling	
	annex emv2 {**                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                    
				use types ErrorLibrary;
				use behavior ErrorLibrary::FailStop;
				error propagations
					display: out propagation {ServiceOmission};
					flows
						es1: error source display;
				end propagations;
				properties
				emv2::hazards => 
					([failure => "No Display";
						description => "No info to display";
					])
					applies to display;
					**};
				
end infotainment_sys;

--implementation of the system design
system implementation infotainment_sys.with_devices
	subcomponents --subcomponents used to build the system
		sys_processor : processor Hardware::MainProcessor.impl; --processor with RAM
		sys_bus : bus Hardware::DefaultBus; --bus for connection
		sys_BT_Adapter : device Hardware::Bluetooth_adapter; --BT device
		sys_Monitor : device HMI::TouchScreen; --Monitor for HMI
		sys_Audio: device HMI::Audio; --Audio system
		sys_controller : process HMI::sys_controller.with_threads; --Central system process
		sys_OS : memory Hardware::ROM; --non volatile system OS storage
		Disp_Application:subprogram Software::Display_Application; --GUI application
	
	connections --connect the required subcomponents as per the system design
		processor_bus_conn : bus access sys_bus->sys_processor.busAcc;
		BT_bus_conn: bus access sys_bus->sys_BT_Adapter.busAcc;
		Monitor_bus_conn : bus access sys_bus->sys_Monitor.busAcc;
		Audio_bus : bus access sys_bus->sys_Audio.busAcc;
	    ROM_conn : bus access sys_bus->sys_OS.busAcc;
	    
	    touch_event_conn: port sys_Monitor.touch_detected->sys_controller.touch_event;
	    display_data_conn: port sys_controller.display_data->sys_Monitor.update_display;
	    Audio_event_conn: port sys_controller.Audio_Control->sys_Audio.config_audio;
	    mic_data_conn: port sys_Audio.record_sound->sys_controller.Mic_inData;
	    speaker_data_conn: port sys_controller.Speaker_outData->sys_Audio.play_sound;
		BT_event_conn: port sys_controller.BT_control->sys_BT_Adapter.control;
		BT_data_comm: port sys_controller.BT_data->sys_BT_Adapter.data_channel;
		
	end infotainment_sys.with_devices;
	
end infotainment_system;